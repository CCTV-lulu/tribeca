<script id="vertexShader" type="x-shader/x-vertex">

  varying vec2 vUv;
  varying vec3 vNormal;

  void main() {

    vUv = uv;
    vNormal = normal.xyz;
    vec4 mvPosition = modelViewMatrix * vec4( position, 1.0 );
    gl_Position = projectionMatrix * mvPosition;

  }

</script>

<script id="fragmentShader" type="x-shader/x-fragment">

  precision highp float;

  varying vec2 vUv;
  varying vec3 vNormal;
  uniform sampler2D map;
  uniform sampler2D LUT;
  uniform float time;
  uniform float darkness;
  uniform float lightness;

  vec3 ColorLookupTable(vec3 InLDRColor)
  {
        // requires a volume texture 16x16x16 unwrapped in a 2d texture 256x16
        // can be optimized by using a volume texture
        vec2 Offset = vec2(0.5 / 256.0, 0.5 / 16.0);
        float Scale = 15.0 / 16.0;

        // Also consider blur value in the blur buffer written by translucency
        float IntB = floor(InLDRColor.b * 14.9999) / 16.0;
        float FracB = InLDRColor.b * 15.0 - IntB * 16.0;

        float U = IntB + InLDRColor.r * Scale / 16.0;
        float V = InLDRColor.g * Scale;

        vec3 RG0 = texture2D(LUT, Offset + vec2(U             , 1.0-V)).rgb;
        vec3 RG1 = texture2D(LUT, Offset + vec2(U + 1.0 / 16.0, 1.0-V)).rgb;

        RG0 = pow(RG0, vec3(0.9));
        RG1 = pow(RG1, vec3(0.9));

        return mix(RG0, RG1, FracB);
  }

  float rand(vec3 co){
      return fract(sin(dot(co.xyz ,vec3(12.9898,78.233+time,44.937))) * 43758.5453);
  }

  float rand2(vec2 co, float freq) {
    return sin(time*freq+dot(co.xy/freq*5. ,vec2(12.9898,78.233)));
  }

  void main() {

    float light = max((vNormal.x-1.0+lightness)*(4.*lightness),0.);
    float dark = max((-vNormal.x-1.0+darkness)*(4.*darkness),0.);

    float r = rand2(vNormal.xy, 10.);
    float r2 = rand2(vNormal.yz, 10.);
    vec2 randUv = vec2( r*0.02, r2*0.02 );
    randUv *= vec2(dark);

    vec3 tex = texture2D( map, vUv + randUv ).xyz;
    tex = ColorLookupTable(tex);

    tex = tex+light-dark;

    gl_FragColor = vec4(tex+vec3(rand(vNormal)*0.15), 1.0);

  }

</script>
<div id="pano" style="position: absolute; left: 0; top: 0; right: 0; bottom: 0; z-index:-1;"></div> 